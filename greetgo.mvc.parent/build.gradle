def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")
def vendorName = "greetgo!"

buildscript {
  repositories {
    if (System.getenv('GG_REPO') != null && System.getenv('GG_REPO').length() > 0) maven {url System.getenv('GG_REPO')}
    jcenter()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-tomcat-plugin:2.3'
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'signing'

  version = '2.0.10'

  sourceCompatibility = 1.8

  group = "kz.greetgo.mvc"

  eclipse.project.natures 'org.springsource.ide.eclipse.gradle.core.nature'
}

subprojects {
  compileJava.options.debugOptions.debugLevel = "source,lines,vars"
  compileTestJava.options.debugOptions.debugLevel = "source,lines,vars"

  repositories {
    if (System.getenv('GG_REPO')?.trim()) maven {url System.getenv('GG_REPO')}
    jcenter()
  }

  dependencies {
    ext.jettyVersion = '9.2+'
    ext.greetgoUtilVersion = '2.0.0'
    ext.servletApiVersion = '3.1.0'
    ext.jacksonVersion = '2.7.3'

    compile 'com.google.code.findbugs:annotations:2.0.1'

    testCompile 'org.testng:testng:6.5.1'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
  }

  compileJava {
    options.encoding = "UTF-8"
  }
  compileTestJava {
    options.encoding = "UTF-8"
  }

  project.eclipse.classpath.file.whenMerged { cp ->
    if (project.hasProperty("sourceCompatibility")) {
      cp.entries.findAll { it.path.contains('JRE_CONTAINER') }.each {

        it.path += "/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/" +
            "JavaSE-$project.sourceCompatibility"
      }
    }
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
          'Implementation-Version': version, 'Implementation-Vendor': vendorName,
          'Built-By': System.getProperty("user.name"), 'Built-At': nowFormattedDate
    }
  }

  sourceSets {
    main {
      java.srcDirs = ["src", "src_resources"]
      resources.srcDirs = ["src", "src_resources"]
    }
    test {
      java.srcDirs = ["test_src", "test_src_resources"]
      resources.srcDirs = ["test_src", "test_src_resources"]
    }
  }

  test { useTestNG() }
}
